# Язык программирования "Гойда"

Русскоязычный язык программирования с синтаксисом, похожим на C.

## Сборки

[![CI Status](https://github.com/Artem468/goida_lang/actions/workflows/linux_build.yml/badge.svg)](https://github.com/Artem468/goida_lang/actions/workflows/linux_build.yml)

[![CI Status](https://github.com/Artem468/goida_lang/actions/workflows/windows_build.yml/badge.svg)](https://github.com/Artem468/goida_lang/actions/workflows/windows_build.yml)

## Отзывы

**Сначала было не привычно после python, но быстро втянулся. API удобный, все работает чётко, а главное свой,
отечественный язык. Приятно осознавать, что можно не зависеть от иностранных разработок.
Советую попробовать!**

## Установка и запуск

```bash
# Клонируйте репозиторий
git clone <url>
cd goida_lang

# Компиляция
cargo build --release

# Запуск файла
cargo run -- run examples/bubble_sort.goida

# Интерактивный режим (REPL)
cargo run -- repl
```

## Синтаксис

### Типы данных

- `число` - целые числа (i64)
- `дробь` - дробные числа (f64)
- `строка` - строки
- `логический` - булевы значения (`истина`, `ложь`)
- `пустота` - None или null в других языках
- `список` - изменяемые список значений
- `массив` - неизменяемый список значений
- `словарь` - коллекция ключ - значение

### Переменные

```goida
// Объявление переменной без типа
имя = "Иван";

// Объявление с указанием типа
возраст: число = 25;
активен: логический = истина;
```

### Арифметические операции

```goida
a = 10;
b = 5;

печать(a + b);  // 15
печать(a - b);  // 5
печать(a * b);  // 50
печать(a / b);  // 2
печать(a % b);  // 0
```

### Логические операции

```goida
результат = (a > b) && (b < 0);
печать(a == b || a != b);
печать(!результат);
```

### Условия

```goida
если (возраст >= 18) {
    печать("Совершеннолетний");
} иначе {
    печать("Несовершеннолетний");
}
```

### Циклы

#### Цикл `пока`

```goida
счетчик = 0;
пока (счетчик < 5) {
    печать("Счетчик: " + счетчик);
    счетчик = счетчик + 1;
}
```

#### Цикл `для`

```goida
для (i = 1; i < 10; i += 1) {
    печать("Число: " + i);
}
```

### Функции

```goida
// Функция без возвращаемого значения
функция приветствие(имя: строка) {
    печать("Привет, " + имя + "!");
}

// Функция с возвращаемым значением
функция сумма(a: число, b: число) -> число {
    вернуть a + b;
}

// Рекурсивная функция
функция факториал(n:число) -> число {
    если (n <= 1) {
        вернуть 1;
    } иначе {
        вернуть n * факториал(n - 1);
    }
}

// Использование функций
приветствие("Мир");
результат = сумма(5, 3);
факт = факториал(5);
```

### Ввод данных

```goida
пользовательский_ввод = ввод();
печать("Вы ввели: " + пользовательский_ввод);
```

### Комментарии

```goida
// Это однострочный комментарий
переменная = 42; // Комментарий в конце строки
```

## Примеры программ

### Hello World

```goida
печать("Привет, мир!");
```

### Калькулятор

```goida
a = 10;
b = 5;

печать("a + b = " + (a + b));
печать("a - b = " + (a - b));
печать("a * b = " + (a * b));
печать("a / b = " + (a / b));
```

### Проверка четности

```goida
функция четное(ч: число) -> логический {
    вернуть ч % 2 == 0;
}

n = 7;
если (четное(n)) {
    печать(n + " четное");
} иначе {
    печать(n + " нечетное");
}
```

### Таблица умножения

```goida
функция таблица_умножения(ч: число) {
    для (i = 1; i < 10; i += 1) {
        печать(ч + " * " + i + " = " + (ч * i));
    }
}

таблица_умножения(7);
```

### ООП (Объектно-ориентированное программирование)

#### Определение класса

```goida
класс Человек {
    // Приватные поля (по умолчанию)
    приватный имя: строка = "Неизвестный";
    приватный возраст: число = 0;
    
    // Публичные поля
    публичный активен: логический = истина;
    
    // Конструктор
    публичный конструктор создать(имя: строка, возраст: число) {
        это.имя = имя;
        это.возраст = возраст;
    }
    
    // Публичные методы
    публичный функция получить_имя() -> строка {
        вернуть это.имя;
    }
    
    публичный функция получить_возраст() -> число {
        вернуть это.возраст;
    }
    
    // Приватные методы (недоступны извне)
    приватный функция секретная_операция() -> строка {
        вернуть "Это секретно!";
    }
}
```

#### Создание объектов и работа с ними

```goida
// Создание объекта
человек = новый Человек("Иван", 25);

// Доступ к публичным полям
человек.активен = ложь;
печать(человек.активен);

// Вызов методов
печать(человек.получить_имя());
печать(человек.получить_возраст());
```

#### Пример с цепочками вызовов

```goida
класс Калькулятор {
    публичный результат: число = 0;
    
    публичный функция сложить(а: число, б: число) -> число {
        это.результат = а + б;
        вернуть это.результат;
    }
    
    публичный функция получить_результат() -> число {
        вернуть это.результат;
    }
}

// Использование
калькулятор = новый Калькулятор();
калькулятор.сложить(5, 3);
печать(калькулятор.получить_результат()); // 8
```

## Ключевые слова

| Русское       | Английский аналог | Описание                      |
|---------------|-------------------|-------------------------------|
| `пусть`       | `let`/`var`       | Объявление переменной         |
| `если`        | `if`              | Условие                       |
| `иначе`       | `else`            | Альтернативная ветка условия  |
| `пока`        | `while`           | Цикл с предусловием           |
| `для`         | `for`             | Цикл со счетчиком             |
| `функция`     | `function`        | Объявление функции            |
| `вернуть`     | `return`          | Возврат значения из функции   |
| `печать`      | `print`           | Вывод на экран                |
| `ввод`        | `input`           | Ввод данных                   |
| `список`      | `list`            | Списки                        |
| `массив`      | `array`           | Массивы                       |
| `словарь`     | `dict`            | Словари                       |
| `тип`         | `type`            | Получить тип объекта          |
| `истина`      | `true`            | Логическое истина             |
| `ложь`        | `false`           | Логическое ложь               |
| `пустота`     | `null`            | нулевой тип                   |
| `число`       | `int`/`number`    | Числовой тип                  |
| `дробь`       | `float`           | Дробный тип                   |
| `строка`       | `string`          | Строковый тип                 |
| `логический`  | `bool`            | Логический тип                |
| `класс`       | `class`           | Объявление класса             |
| `новый`       | `new`             | Создание объекта              |
| `приватный`   | `private`         | Приватный модификатор доступа |
| `публичный`   | `public`          | Публичный модификатор доступа |
| `конструктор` | `constructor`     | Объявление конструктора       |
| `это`         | `this`            | Ссылка на текущий объект      |

## Операторы

| Символ | Описание                |
|--------|-------------------------|
| `+`    | Сложение/конкатенация   |
| `-`    | Вычитание/унарный минус |
| `*`    | Умножение               |
| `/`    | Деление                 |
| `%`    | Остаток от деления      |
| `==`   | Равенство               |
| `!=`   | Неравенство             |
| `>`    | Больше                  |
| `<`    | Меньше                  |
| `>=`   | Больше или равно        |
| `<=`   | Меньше или равно        |
| `&&`   | Логическое И            |
| `\|\|` | Логическое ИЛИ          |
| `!`    | Логическое НЕ           |
| `=`    | Присваивание            |


### Запуск тестов

```bash
cargo test
```
